#= depend_on_asset "editor.html"

ContactApp = angular.module('ContactApp', ['ngResource'])

ContactApp.config ($httpProvider) ->
  authToken = $("meta[name=\"csrf-token\"]").attr("content")
  $httpProvider.defaults.headers.common["X-CSRF-TOKEN"] = authToken

ContactApp.factory 'Contacts', ($resource) ->
    @service = $resource '/api/contacts/:id',
      {id: '@id'}
      update: { method: 'PUT' }

ContactApp.controller 'ContactControl', ($scope, Contacts) ->
  $scope.init = ->
    $scope.viewing = false
    $scope.editing = false
    $scope.contacts = Contacts.query()

  capitalize = (str) ->
    str.charAt(0).toUpperCase() + str.slice(1)

  formatErrors = (errors) ->
    all = for field, error of errors.errors
      "#{capitalize(field)} #{error}."

    all.join("<br/>")

  $scope.displayErrors = (response) ->
    $scope.errorMessage = formatErrors(response.data)
    $scope.editing = true
    $scope.viewing = false

  $scope.newContact = ->
    $scope.showEditor()

  $scope.save = (contact) ->
    saved = if contact.id
      contact.$update {},
        ->
          $scope.contact = contact
          $scope.showContact(contact)
        $scope.displayErrors
    else
      newContact = new Contacts(contact)
      newContact.$save {},
        (c) ->
          $scope.contacts.push c
          $scope.showContact(c)
        $scope.displayErrors

  $scope.clearErrors = ->
    $scope.errorMessage = null

  $scope.showEditor = (contact) ->
    $scope.viewing = false
    $scope.editing = true
    $scope.contact = contact

  $scope.showContact = (contact) ->
    $scope.viewing = true
    $scope.editing = false
    $scope.contact = contact

  $scope.truncate = (string, max) ->
    return string if string.length <= max
    string.substring(0, max-3) + "..."

ContactApp.directive 'editor', () ->
  restrict: 'E',
  templateUrl: '<%= asset_path("editor.html") %>'
